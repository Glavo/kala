buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    group 'org.glavo'
    version '0.1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    if (JavaVersion.current().isJava9Compatible()) {
        sourceCompatibility = 9
        targetCompatibility = 9
        sourceSets.main.java.srcDirs = ['src/main/java', 'src/main/module']
        sourceSets.main.kotlin.srcDirs = ['src/main/module', 'src/main/java', 'src/main/kotlin']
        compileJava.configure {
            destinationDir = compileKotlin.destinationDir
            doFirst {
                options.compilerArgs = ['--module-path', classpath.asPath,]
                classpath = files()
            }
        }
    } else {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
        sourceSets.main.java.srcDirs = ['src/main/java']
        sourceSets.main.kotlin.srcDirs = ['src/main/java', 'src/main/kotlin']
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }
    compileKotlin {
        kotlinOptions.jvmTarget = "1.6"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.6"
    }
}